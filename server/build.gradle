apply from: "$rootDir/gradle/javaModule.gradle"

archivesBaseName = 'crate-server'

configurations {
    // export of test so that other modules can use the utilities there as well.
    testOutput
}

dependencies {
    compile project(':libs:shared')
    implementation project(':es:es-server')
    compile project(':libs:dex')
    compile project(':libs:sql-parser')
    compile project(':libs:pgwire')
    compile "com.tdunning:t-digest:3.2"
    compile "org.hdrhistogram:HdrHistogram:2.1.9"

    compile "io.netty:netty-buffer:${versions.netty4}"
    compile "io.netty:netty-codec:${versions.netty4}"
    compile "io.netty:netty-codec-http:${versions.netty4}"
    compile "io.netty:netty-common:${versions.netty4}"
    compile "io.netty:netty-handler:${versions.netty4}"
    compile "io.netty:netty-resolver:${versions.netty4}"
    compile "io.netty:netty-transport:${versions.netty4}"
    compile "io.netty:netty-transport-native-epoll:${versions.netty4}:linux-x86_64"

    compile "com.google.guava:guava:${versions.guava}"
    compile "com.amazonaws:aws-java-sdk-s3:${versions.aws}"
    compile "org.apache.commons:commons-math3:${versions.commonsmath}"
    compile "com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${versions.jackson}"
    compile "com.fasterxml.jackson.core:jackson-databind:${versions.jackson}"
    // Needed by aws-java-sdk-s3 in Java 9
    compile "javax.xml.bind:jaxb-api:${versions.jaxb_api}"

    testCompile project(':integration-testing')
    testCompile project(path: ':libs:dex', configuration: 'testOutput')
    testCompile 'org.skyscreamer:jsonassert:1.3.0'
    testCompile "org.hamcrest:hamcrest:${versions.hamcrest}"
    testCompile "org.slf4j:slf4j-api:${versions.slf4j}"
    testCompile "org.postgresql:postgresql:${versions.jdbc}"
    testCompile 'com.pholser:junit-quickcheck-core:0.9'
    testCompile 'com.pholser:junit-quickcheck-generators:0.9'
    // These plugins are dynamically loaded and need to be present here.
    // They are required by 1) tests in :server 2) modules depending on :server.
    // They also need to be added in :app because :server is declared non-transitive
    // in :app (=dependencies of :server won't be included).
    compile project(":plugins:es-repository-url")
    compile project(":plugins:es-repository-s3")
    compile project(":plugins:es-repository-azure")
    compile project(":plugins:es-analysis-common")
}
//noinspection GroovyAssignabilityCheck
task testOutputJar (type: Jar) {
    classifier = 'server-testing'
    from sourceSets.test.output
}

artifacts {
    testOutput testOutputJar
}

test {
    outputs.dir("$projectDir/data")

    jacoco.excludes = ["*Test*"]

    // make sure sources are first on classpath because we do override some class(es) (currently: lucene's AssertingLeafReader)
    classpath = sourceSets.main.output + sourceSets.test.output + configurations.testCompile
}

clean.dependsOn('cleanTest')
