apply from: "$rootDir/gradle/javaModule.gradle"
apply plugin: 'groovy'

archivesBaseName = 'crate-sql'

configurations {
    benchmarksCompile.extendsFrom testCompile
    benchmarksRuntime.extendsFrom testRuntime, benchmarksCompile
}

dependencies {
    compile project(':core')
    compile project(':blob')
    compile project(':sql-parser')
    compile "com.amazonaws:aws-java-sdk-s3:${versions.aws}"
    compile "org.apache.commons:commons-math3:${versions.commonsmath}"
    testCompile project(':testing')
    testCompile 'org.skyscreamer:jsonassert:1.3.0'
    testCompile "org.hamcrest:hamcrest-all:${versions.hamcrest}"
    testCompile('io.crate:crate-jdbc:2.1.0') {
        exclude group: 'net.java.dev.jna', module: 'jna'
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.slf4j', module: 'jcl-over-slf4j'
    }
    benchmarksCompile 'com.carrotsearch:junit-benchmarks:0.7.2'
    benchmarksCompile 'com.h2database:h2:1.3.173'
}

test {
    testLogging.exceptionFormat = 'full'
    outputs.dir("$projectDir/data")

    jacoco.excludes = ["*Test*"]

    // make sure sources are first on classpath because we do override some class(es) (currently: lucene's AssertingLeafReader)
    classpath = sourceSets.main.output + sourceSets.test.output + configurations.testCompile
}

clean.dependsOn(cleanTest)

sourceSets {
    test {
        resources {
            srcDir 'src/test/resources'

        }
    }
    benchmarks {
        java {
            srcDir 'src/benchmarks/java'
            compileClasspath += main.output + test.output + configurations.benchmarksCompile
            runtimeClasspath += main.output + test.output + configurations.benchmarksRuntime
        }
    }
}

task benchmarks(type: Test) {
    description = "Run internal benchmarks"

    testClassesDir = project.sourceSets.benchmarks.output.classesDir

    // move lucene-test-framework to the beginning of the classpath
    def cp = project.sourceSets.benchmarks.runtimeClasspath
    def testFrameworkEntry = cp.find({ it.name.contains("lucene-test-framework") })
    if (testFrameworkEntry != null) {
        cp = files(testFrameworkEntry) + cp.filter({ !it.name.contains("lucene-test-framework") })
    }
    classpath = cp
    def benchmark_dir = project.projectDir.path + '/reports/bench/'

    // Properties for JUnit Benchmarks
    systemProperty 'jub.consumers', 'CONSOLE,H2,XML'
    systemProperty 'jub.db.file', benchmark_dir + 'results-sql'
    systemProperty 'jub.xml.file', benchmark_dir + 'results-sql.xml'
    systemProperty 'jub.charts.dir', benchmark_dir

    // force run, see: http://gradle.1045684.n5.nabble.com/how-does-gradle-decide-when-to-run-tests-td3314172.html
    outputs.upToDateWhen { false }

    jacoco {
        enabled = false
    }
}


idea {
    module {
        testSourceDirs += file('src/benchmarks/java')
        // put additional dependencies on the classpath
        scopes.TEST.plus += [configurations.benchmarksCompile]
        scopes.TEST.plus += [configurations.benchmarksRuntime]
    }
}

