apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'

archivesBaseName = 'crate-sql'

// we do not want to have elasticsearch here, since we provide it in :es
configurations {
    all*.exclude group: 'org.elasticsearch'
    sharedCompile.extendsFrom compile
}

dependencies {
    compile project(':es')
    compile project(':core')
    compile project(':blob')
    compile project(':sql-parser')
    compile 'com.amazonaws:aws-java-sdk:1.9.34'
    compile 'org.apache.commons:commons-math3:3.4.1'
    testCompile project(':testing')
    testCompile 'org.skyscreamer:jsonassert:1.2.0'
    testCompile ('org.powermock:powermock-module-junit4:1.6.1') {
        exclude group: 'junit', module: 'junit'
    }
    testCompile ('org.powermock:powermock-api-mockito:1.6.1') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.mockito', module: 'mockito-all'
    }
    testCompile 'org.javassist:javassist:3.19.0-GA'
}

buildscript {
    repositories {
        mavenCentral()
    }
}

test {
    // force run, see: http://gradle.1045684.n5.nabble.com/how-does-gradle-decide-when-to-run-tests-td3314172.html
    outputs.upToDateWhen { false }

    testLogging.exceptionFormat = 'full'
    exclude('io/crate/benchmark/**')

    // fix powermock issue - causing VerifyErrors being thrown
    // see: https://issues.jboss.org/browse/JASSIST-228
    jvmArgs '-XX:-UseSplitVerifier'

    jacoco {
        excludes = [
            "*Test*",
            "*Benchmark*"
        ]
    }
}


sourceSets {
    test {
        resources {
            srcDir 'src/test/java'
            include '**/*.Plugin'
            include '**/*.rst'
            include '**/*.json'
            include '**/*.sql'
            include '**/*.html'
            include '**/*.jsonp'
        }
    }
    shared {
        java {
            srcDir "src/main/java/"
            include "io/crate/action/sql/SQL*"
        }
    }
}
