apply from: "$rootDir/gradle/javaModule.gradle"

archivesBaseName = 'crate-sql-parser'

ext.antlr = [
        source : "src/main/antlr",
        output : "src/main/java/io/crate/sql/parser/antlr/v4",
        package: 'io.crate.sql.parser.antlr.v4'
]

configurations {
    antlr4
}

dependencies {
    antlr4 "org.antlr:antlr4:${versions.antlr}"
    compile "org.antlr:antlr4-runtime:${versions.antlr}"

    compile "com.google.code.findbugs:jsr305:${versions.jsr305}"
    compile "com.google.guava:guava:${versions.guava}"

    testCompile "org.hamcrest:hamcrest:${versions.hamcrest}"
    testCompile "junit:junit:${versions.junit}"
}

task antlrOutputDir {
    doLast {
        mkdir(antlr.output)
    }
}

task generateGrammarSource(dependsOn: antlrOutputDir, type: JavaExec) {
    inputs.files(fileTree(antlr.source))
    outputs.dir file(antlr.output)

    def grammars = fileTree(antlr.source).include('**/*.g4')

    main = 'org.antlr.v4.Tool'
    classpath = configurations.antlr4
    args = ["-o", "${antlr.output}", "-visitor", "-package", antlr.package, grammars.files].flatten()
}

test {
    // show standard out and standard error of the test JVM(s) on the console
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'

    jacoco {
        excludes = [
                "*Test*",
                "TreeAssertions*",
                "TreePrinter*"
        ]
    }
}

tasks.withType(JavaCompile) {
    it.dependsOn('generateGrammarSource')
}

clean {
    delete antlr.output
}
