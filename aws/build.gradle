apply plugin: 'java'

archivesBaseName = 'crate-aws'
group = 'io.crate'
description = 'Crate.IO AWS Plugin'

configurations {
    all*.exclude group: 'org.elasticsearch'
}

dependencies {
    compile project(':es')
    compile 'com.amazonaws:aws-java-sdk-ec2:1.10.12'
    compile 'com.amazonaws:aws-java-sdk-s3:1.10.12'
}

sourceSets {
    main {
        java {
            srcDir '../es/upstream/plugins/cloud-aws/src/main/java'
        }
    }
}

task writePropertiesFile (dependsOn: ['getVersion']) << {
    def file = new File("${buildDir}/plugin-descriptor.properties");
    ant.propertyfile(file:file.path, comment:'Plugin Descriptor') {
        ant.entry(key:'name', value:project.archivesBaseName)
        ant.entry(key:'description', value:project.description)
        ant.entry(key:'version', value:project.version)
        ant.entry(key:'jvm', value:true)
        ant.entry(key:'classname', value:'org.elasticsearch.plugin.cloud.aws.CloudAwsPlugin')
        ant.entry(key:'elasticsearch.version', value:'2.1.2')
        ant.entry(key:'java.version', value:JavaVersion.current())
    }
}

processResources.dependsOn writePropertiesFile

// get version and set it on the project, so it gets appended to the jar name
task getVersion(dependsOn: [':core:getVersion']) {
    doFirst {
        project.version = project(':core').getVersion.version
    }
}

jar {
    doLast {
        manifest {
            attributes("Implementation-Title": "Crate.IO AWS Plugin", "Implementation-Version": project.version)
        }
    }
}
jar.dependsOn(getVersion, writePropertiesFile)
