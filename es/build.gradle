apply plugin: 'java'

repositories {
    mavenCentral()
}

configurations { lucenetest }

idea {
    module {
        scopes.TEST.plus.add(0, configurations.lucenetest)
    }
}

compileJava {
    options.compilerArgs << '-XDignore.symbol.file'
    options.fork = true // may not needed on 1.8
    options.forkOptions.executable = 'javac' // may not needed on 1.8
}

//generated in upstream directory with:
// mvn dependency:tree|grep -o ' .- .*:compile'|cut -d ' ' -f 3|sort|gsed "s/\(.*\):jar:\(.*\):compile/compile '\1:\2'/g"
// TODO: excludes e.g. xerces
dependencies {

    compile 'com.carrotsearch:hppc:0.5.3'
    compile 'com.fasterxml.jackson.core:jackson-core:2.4.1.1'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.4.1'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-smile:2.4.1'
    compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.4.1'
    compile 'com.github.spullara.mustache.java:compiler:0.8.13'
    compile 'com.google.guava:guava:17.0'
    compile 'com.ning:compress-lzf:1.0.2'

    compile 'org.codehaus.groovy:groovy-all:2.3.2'

    compile 'com.spatial4j:spatial4j:0.4.1'
    compile 'com.tdunning:t-digest:3.0'
    compile 'com.vividsolutions:jts:1.13'
    compile 'io.netty:netty:3.9.1.Final'
    compile 'joda-time:joda-time:2.3'
    compile 'log4j:log4j:1.2.17'
    compile 'net.java.dev.jna:jna:4.1.0'
    // also updated dependencies in client dependencies
    // when updating lucene version!
    compile 'org.apache.lucene:lucene-analyzers-common:4.9.0'
    compile 'org.apache.lucene:lucene-codecs:4.9.0'
    compile 'org.apache.lucene:lucene-core:4.9.0'
    compile 'org.apache.lucene:lucene-expressions:4.9.0'
    compile 'org.apache.lucene:lucene-grouping:4.9.0'
    compile 'org.apache.lucene:lucene-highlighter:4.9.0'
    compile 'org.apache.lucene:lucene-join:4.9.0'
    compile 'org.apache.lucene:lucene-memory:4.9.0'
    compile 'org.apache.lucene:lucene-misc:4.9.0'
    compile 'org.apache.lucene:lucene-queries:4.9.0'
    compile 'org.apache.lucene:lucene-queryparser:4.9.0'
    compile 'org.apache.lucene:lucene-sandbox:4.9.0'
    compile 'org.apache.lucene:lucene-spatial:4.9.0'
    compile 'org.apache.lucene:lucene-suggest:4.9.0'
    // keep sigar out, see below
    // compile 'org.fusesource:sigar:1.6.4'
    compile 'org.joda:joda-convert:1.2'
    compile 'org.mvel:mvel2:2.2.0.Final'
    compile 'org.slf4j:slf4j-api:1.6.2'

    // we add the sigar libs as system dep since it is checked in
    compile fileTree(dir: 'upstream/lib/sigar/', include: '*.jar')

    // by hand
    testCompile 'org.apache.mahout:mahout-core:0.9'
    testCompile ('org.apache.httpcomponents:httpclient:4.3.3') {
        exclude group: 'commons-codec', module: 'commons-codec'
    }
    testCompile 'commons-codec:commons-codec:1.9'
    testCompile 'org.hamcrest:hamcrest-all:1.3'

    lucenetest 'junit:junit:4.11'
    lucenetest ('org.apache.lucene:lucene-test-framework:4.9.0') {
        exclude group: 'junit', module: 'junit'
    }

    testCompile ('com.carrotsearch.randomizedtesting:randomizedtesting-runner:2.1.2') {
        exclude group: 'junit', module: 'junit'
    }
}

sourceSets.test.compileClasspath = configurations.lucenetest + sourceSets.test.compileClasspath
sourceSets.test.runtimeClasspath = configurations.lucenetest + sourceSets.test.runtimeClasspath

test {
    enabled = false
    jacoco {
        enabled = false
    }
}

sourceSets {
    main {
        java {
            srcDir 'upstream/src/main/java'
        }
        resources {
            srcDir 'upstream/src/main/resources'
            include '**/*.*'
        }
        resources {
            srcDir 'upstream/src/main/java'
            include '**/*.json'
            include '**/*.yml'
        }
    }
    test {
        java {
            srcDir 'upstream/src/test/java'
        }
        resources {
            srcDir 'upstream/src/test/resources'
            include '**/*.*'
        }
        resources {
            srcDir 'upstream/src/test/java'
            include '**/*.json'
            include '**/*.yml'
            include '**/*.txt'
        }
    }
}
