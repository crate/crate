def crateDist = file("$projectDir/tmp/crate/")

apply plugin: 'base'

evaluationDependsOn(':app')
evaluationDependsOn(':es:es-repository-hdfs')

task bootstrap(type: Exec) {
    inputs.file "$projectDir/bootstrap.sh"
    outputs.dir "$projectDir/.venv"

    commandLine 'sh', "$projectDir/bootstrap.sh"
}

task unpackDistTar(dependsOn: [project(':app').installDist]) {
    doLast {
        copy {
            from(project(':app').installDist.destinationDir) {
                includeEmptyDirs = true
            }
            into crateDist
        }
    }
    outputs.dir crateDist
}

task lessLogging(dependsOn: unpackDistTar) {
    doLast {
        def file = new File("$projectDir/tmp/crate/config/log4j2.properties")
        file.write(file.text.replaceAll('rootLogger.level = info', 'rootLogger.level: warn'))
    }
}

task ignoreDiskThreshold(dependsOn: unpackDistTar) {
    doLast {
        def file = new File("$projectDir/tmp/crate/config/crate.yml")
        file.write(file.text.replaceAll(
                '# cluster.routing.allocation.disk.threshold_enabled: true',
                'cluster.routing.allocation.disk.threshold_enabled: false')
        )
    }
}

task hdfsTest(type: Exec) {
    commandLine "$projectDir/bin/test-hdfs"
}

task monitoringTest(type: Exec) {
    commandLine "$projectDir/bin/test-jmx"
}

task itest(type: Exec) {
    commandLine "$projectDir/bin/test-docs", '-vvvxt', '!process_test'
}

task gtest(type: Exec) {
    commandLine "$projectDir/bin/test-docs", '-vvvxt', 'process_test'
}

task dnsDiscoveryTest(type: Exec) {
    commandLine "$projectDir/bin/test-dns-discovery"
}

task buildDocs(type: Exec, dependsOn: bootstrap) {
    commandLine "$projectDir/bin/sphinx"
}

task developDocs(type: Exec, dependsOn: bootstrap) {
    commandLine "$projectDir/bin/sphinx", "dev"
}

hdfsTest.dependsOn(bootstrap, lessLogging, ignoreDiskThreshold,
        project(':es:es-repository-hdfs').blackBoxTestJar)
monitoringTest.dependsOn(bootstrap, lessLogging, ignoreDiskThreshold)
itest.dependsOn(bootstrap, lessLogging, ignoreDiskThreshold)
gtest.dependsOn(bootstrap, lessLogging, ignoreDiskThreshold)
dnsDiscoveryTest.dependsOn(bootstrap, lessLogging, ignoreDiskThreshold)

task cleanDocs {
    doLast {
        FileCollection toDelete = files(
                crateDist,
                "$projectDir/docs/_out/html",
        )
        toDelete.each {
            File file -> delete file
        }
    }
}
clean.dependsOn([cleanDocs, cleanBootstrap, cleanUnpackDistTar])
