def crateDist = file("$projectDir/tmp/crate/")


task bootstrap (type:Exec) {
    inputs.file "$projectDir/bootstrap.py"
    outputs.file "$projectDir/bin/buildout"

    commandLine 'python2.7', "$projectDir/bootstrap.py"
}

task unpackDistTar (dependsOn: project(':app').distTar) << {
    copy {
        includeEmptyDirs = false
        from(tarTree(project(':app').distTar.archivePath)) {
            eachFile { details ->
                details.path =
                    details.path.substring(details.relativePath.segments[0].length())
            }
        }
        into crateDist
    }

}

task buildout (type:Exec, dependsOn: bootstrap) {
    commandLine "$projectDir/bin/buildout", '-N'
}

task itest (type:Exec){

    executable = "$projectDir/bin/test"
}

task lessLogging (dependsOn: unpackDistTar) << {
    def file = new File("$projectDir/tmp/crate/config/logging.yml")
    file.write(file.text.replaceAll('rootLogger: INFO', 'rootLogger: WARN'))
}

itest.dependsOn(unpackDistTar, buildout, lessLogging)

task clean << {
    FileCollection toDelete = files(crateDist,
        "$projectDir/out/text",
        "$projectDir/out/html",
        "$projectDir/parts",
        "$projectDir/.installed.cfg",
        "$projectDir/bin",
        "$projectDir/develop-eggs")
    toDelete.each {File file ->
        delete file
    }
}
