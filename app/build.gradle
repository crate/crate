apply plugin:'distribution'
apply plugin: 'java'

archivesBaseName = 'crate-app'

configurations {
    all*.exclude group: 'org.elasticsearch'
}

task getVersion (dependsOn: 'classes') << {
    def stdout = new ByteArrayOutputStream()
    javaexec {
        classpath = sourceSets.main.runtimeClasspath
        main = 'org.cratedb.Version'
        standardOutput = stdout
    }
    ext.version = stdout.toString().trim().split(" ")[1].replace(',','')
}

task dist {
    dependsOn ':docs:itest', distTar, distZip
}

distTar {
    dependsOn ":docs:sphinx", getVersion
    compression = Compression.GZIP
    baseName = 'crate'
    extension = 'tar.gz'
    doFirst {
        version = getVersion.version
    }
}

distZip {
    dependsOn ":docs:sphinx", getVersion
    baseName = 'crate'
    doFirst {
        version = getVersion.version
    }
}

ext {
    downloadDir = new File(buildDir, 'downloads')
    plugin_head_version = '3b6740cfd0a79c60516ce8fbd78f018c1f38e08e'
    plugin_bigdesk_version = '2.2.2c'
    plugin_segmentspy_version = '0.2.0'
}

evaluationDependsOn(':es')

dependencies {
    compile project(':core')
    compile project(':es')
    compile project(':sql')
    compile project(':admin-ui')
    compile project(':inout')
    compile project(':blob')
    testCompile 'com.carrotsearch.randomizedtesting:randomizedtesting-runner:2.0.10'
}

installDist.dependsOn(jar, ":docs:sphinx")

def docsDir = project(":docs").projectDir
def rootDir = project.parent.projectDir

distributions {
    main {
        baseName = 'crate'
        contents {
            into("") {
                from "$rootDir"
                include 'NOTICE'
                include 'CHANGES.txt'
            }
            into("docs") {
                from "${docsDir}/out/text"
                include '**/*.txt'
                exclude '.doctrees'
            }
            into("lib") {
                from project.configurations.runtime
                // sigar gets included from the checked in runtime dependency in :es:
                exclude 'sigar-*.jar'
            }
            into("lib") {
                from jar
            }
            into("plugins/docs/_site") {
                from "${docsDir}/out/html"
            }
            into('plugins/crate-admin/_site'){
                from new File(project(":admin-ui").projectDir, 'crate-admin/dist')
            }
            into('plugins') {
                from 'plugins'
            }
        }
    }
}


def download (String url, String name){
    downloadDir.mkdirs()
    def dest = new File(downloadDir, name)
    ant.get(
            src: url,
            verbose: true,
            usetimestamp: true,
            skipexisting: true,
            dest: dest
    )
    return dest;
}

def extractTopfolder(File src, String trg){
    copy {
        includeEmptyDirs = false
        from(zipTree(src)) {
            eachFile { details ->
                details.path =
                    details.path.substring(details.relativePath.segments[0].length())
            }
        }
        into trg
    }
}

task downloadPlugins << {
    def dest = download(
            "https://github.com/mobz/elasticsearch-head/archive/${plugin_head_version}.zip",
            "head_${plugin_head_version}.zip")
    extractTopfolder(dest, 'plugins/head/_site')
    dest = download(
            "https://github.com/crate/bigdesk/archive/v${plugin_bigdesk_version}.zip",
            "bigdesk_${plugin_bigdesk_version}.zip"
            )
    extractTopfolder(dest, 'plugins/bigdesk/_site')
    dest = download(
            "https://github.com/polyfractal/elasticsearch-segmentspy/archive/v${plugin_segmentspy_version}.zip",
            "segementspy_${plugin_segmentspy_version}.zip"
    )
    extractTopfolder(dest, 'plugins/segmentspy')
}

processResources.dependsOn downloadPlugins

task(runDebug, dependsOn: 'classes', type: JavaExec) {
    main = 'org.cratedb.bootstrap.CrateF'
    debug = true
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = '512m'
}

sourceSets {
    test {
        resources {
            srcDir 'src/test/java'
        }
    }
}
