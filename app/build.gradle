buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}

apply plugin: 'distribution'
apply plugin: 'java'
apply plugin: 'eu.appsatori.fatjar'
apply plugin: 'signing'
apply plugin: 'maven'

archivesBaseName = 'crate-app'
group = "io.crate"


configurations {
    all*.exclude group: 'org.elasticsearch'
    compileNotTransitive {
        transitive = false
    }
}

evaluationDependsOn(':core')
evaluationDependsOn(':es')
evaluationDependsOn(':sql')
evaluationDependsOn(':sql-parser')
evaluationDependsOn(':blob')
evaluationDependsOn(':udc')
evaluationDependsOn(':client')

dependencies {
    compile project(':core')
    compile project(':es')
    compile project(':sql')
    compile project(':admin-ui')
    compile project(':blob')
    compile project(':udc')
    compile project(':client')

    testCompile ('com.carrotsearch.randomizedtesting:randomizedtesting-runner:2.1.10') {
        exclude group: 'junit', module: 'junit'
    }
    testCompile 'junit:junit:4.11'


    compileNotTransitive project(':core')
    compileNotTransitive project(':es')
    compileNotTransitive project(':sql')
    compileNotTransitive project(':sql-parser')
    compileNotTransitive project(':admin-ui')
    compileNotTransitive project(':blob')
    compileNotTransitive project(':udc')
    compileNotTransitive project(':client')
}

task release(dependsOn: 'distTar') << {
    logger.info("released version: " + project(':core').getVersion.version);
}

task nightly(dependsOn: 'distTar') << {
    logger.info("nightly version: " + project(':core').getVersion.version);
}


fatJar {
    dependsOn ':core:getVersion'
    doFirst {
        manifest.attributes 'Implementation-Version': project(':core').getVersion.version
    }
    manifest {
        attributes 'Main-Class': 'io.crate.bootstrap.CrateF',
        'Implementation-Title': 'Crate',
        'Built-Date': new Date(),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion
    }
}

task dist {
    dependsOn ':docs:itest', distTar, distZip
}

task myJavadocs(type: Javadoc, dependsOn: processResources) {
    classpath = configurations.compile
    source = sourceSets.main.allJava + project(':core').sourceSets.main.allJava + project(':sql').sourceSets.main.allJava + project(':sql-parser').sourceSets.main.allJava + project(':blob').sourceSets.main.allJava + project(':udc').sourceSets.main.allJava + project(':client').sourceSets.main.allJava
}

task javadocJar (type: Jar, dependsOn: [myJavadocs]) {
    classifier = 'javadoc'
    from myJavadocs.destinationDir
    manifest {
        attributes("Implementation-Title": "Crate.IO")
    }
}

task buildJavadocJar (dependsOn: [':core:getVersion', myJavadocs] ) << {
    ext.version = project(':core').getVersion.version
    project.version = ext.version
    tasks.javadocJar.execute()
}

task sourceJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource + project(':core').sourceSets.main.allJava + project(':sql').sourceSets.main.allJava + project(':sql-parser').sourceSets.main.allJava + project(':blob').sourceSets.main.allJava + project(':udc').sourceSets.main.allJava + project(':client').sourceSets.main.allJava
    manifest {
        attributes("Implementation-Title": "Crate.IO")
    }
}

task buildSourceJar (dependsOn: [':core:getVersion'] ) << {
    ext.version = project(':core').getVersion.version
    project.version = ext.version
    tasks.sourceJar.execute()
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

task signJars (type : Sign, dependsOn: [jar, buildSourceJar, buildJavadocJar]) {
    sign configurations.archives
}

install.dependsOn([jar, ':core:getVersion', buildSourceJar, buildJavadocJar])
install {
    repositories {
        mavenInstaller {
            pom.whenConfigured {
                pom -> pom.dependencies.clear()
                project.parent.subprojects.findAll{ it.name != 'app'}.each {
                    if (it.hasProperty('install')) {
                        pom.dependencies.addAll(it.install.repositories.mavenInstaller.pom.getEffectivePom().dependencies.findAll { it.groupId != 'io.crate'})
                    }
                }
            }
            pom.project {
                artifactId 'crate'
                name 'crate'
                description 'Crate.IO'
                url 'https://crate.io'
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'crate'
                        name 'Crate Developers'
                        email 'office@crate.io'
                    }
                }
            }
        }
    }
}

project.ext.bintrayUsername = project.hasProperty('bintrayUsername') ? bintrayUsername : ""
project.ext.bintrayPassword = project.hasProperty('bintrayPassword') ? bintrayPassword : ""

uploadArchives.dependsOn([jar, ':core:getVersion', buildSourceJar, buildJavadocJar, signJars])
uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
                if (project.ext.bintrayUsername.length() == 0 || project.ext.bintrayPassword.length() == 0) {
                    throw new StopExecutionException("uploadArchives cannot be called without bintray username and password")
                }
            }
            pom.whenConfigured {
                pom -> pom.dependencies.clear()
                project.parent.subprojects.findAll{ it.name != 'app'}.each {
                    if (it.hasProperty('install')) {
                        pom.dependencies.addAll(it.install.repositories.mavenInstaller.pom.getEffectivePom().dependencies.findAll { it.groupId != 'io.crate'})
                    }
                }
            }
            pom.project {
                artifactId 'crate'
                name 'crate'
                description 'Crate.IO'
                url 'https://crate.io'
                scm {
                    url 'https://github.com/crate/crate'
                    connection 'scm:git:git://github.com/crate/crate.git'
                    developerConnection 'scm:git:ssh:git@github.com:crate/crate.git'
                }
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/license/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'crate'
                        name 'Crate Developers'
                        email 'office@crate.io'
                    }
                }
            }
            pom {
                artifactId = 'crate'
            }
            repository(id: 'crate', url: 'https://api.bintray.com/maven/crate/crate/crate') {
                authentication(userName: project.ext.bintrayUsername, password: project.ext.bintrayPassword)
            }
        }
    }
}

distTar {
    dependsOn ':core:getVersion'
    compression = Compression.GZIP
    baseName = 'crate'
    extension = 'tar.gz'
    doFirst {
        version = project(':core').getVersion.version
    }
}

distZip {
    dependsOn ':core:getVersion'
    baseName = 'crate'
    doFirst {
        version = project(':core').getVersion.version
    }
}

ext {
    downloadDir = new File(buildDir, 'downloads')
    plugin_crateadmin_version = '0.11.5'
    crash_version = '0.11.5'
}

def rootDir = project.parent.projectDir

distributions {
    main {
        baseName = 'crate'
        contents {
            into("") {
                from "$rootDir"
                include 'LICENSE.txt'
                include 'NOTICE'
                include 'README.rst'
                include 'CHANGES.txt'
            }
            into("lib") {
                from project.configurations.runtime
                // sigar gets included from the checked in runtime dependency in :es:
                exclude 'sigar-*.jar'

                exclude 'crate-blob.jar'
                exclude 'crate-core.jar'
                exclude 'crate-sql-parser.jar'
                exclude 'crate-sql.jar'
                exclude 'crate-udc.jar'
                exclude 'crate-client.jar'
            }
            into("lib") {
                from jar
            }
            into('plugins') {
                from 'plugins'
            }
            into('bin') {
                from ("${downloadDir}/crash_standalone") {
                    fileMode = 0755
                }
            }
        }
    }
}

jar {
    dependsOn ':core:getVersion'

    doFirst {
        manifest.attributes 'Implementation-Version': project(':core').getVersion.version
        project.version = project(':core').getVersion.version
        def serviceDir = file("$buildDir/META-INF/services")
        serviceDir.deleteDir()
        serviceDir.mkdirs()

        // copy all service files from deps to buildDir
        for(file in configurations.compileNotTransitive) {
            zipTree(file).matching{ include 'META-INF/services/*' }.each { f ->
                new File(serviceDir, f.name) << f.getText("UTF-8") << "\n"
            }
        }
    }

    from(configurations.compileNotTransitive.collect { it.isDirectory() ? it : zipTree(it) }){
        exclude 'META-INF/**' // Don't let Gradle merge service files
    }

    // include service files from the buildDir
    from fileTree(buildDir).matching{ include 'META-INF/services/*' }
}

def download (String url, String name){
    downloadDir.mkdirs()
    def dest = new File(downloadDir, name)
    ant.get(
            src: url,
            verbose: true,
            usetimestamp: true,
            skipexisting: true,
            dest: dest
    )
    return dest;
}

def extractTopfolder(File src, String trg){
    copy {
        includeEmptyDirs = false
        FileTree ft
        if(src.name.endsWith('.zip')) {
            ft = zipTree(src)
        }
        if(src.name.endsWith('.tar.gz')) {
            ft = tarTree(src)
        }
        from(ft) {
            eachFile { details ->
                details.path =
                    details.path.substring(details.relativePath.segments[0].length())
            }
        }
        into trg
    }
}

task downloadPlugins << {
    def dest = download(
            "https://cdn.crate.io/downloads/releases/crate-admin-${plugin_crateadmin_version}.tar.gz",
            "crate-admin-${plugin_crateadmin_version}.tar.gz"
    )
    extractTopfolder(dest, 'plugins/crate-admin/_site')
}

task downloadCrash << {
    def dest = download(
         "https://cdn.crate.io/downloads/releases/crash_standalone_${crash_version}",
         "crash_standalone"
    )
}

processResources.dependsOn(downloadPlugins, downloadCrash)

task(runDebug, dependsOn: 'classes', type: JavaExec) {
    main = 'io.crate.bootstrap.CrateF'
    debug = true
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = '512m'
}

task(run, dependsOn: 'classes', type: JavaExec) {
    main = 'io.crate.bootstrap.CrateF'
    debug = false
    classpath = sourceSets.main.runtimeClasspath
    maxHeapSize = '512m'
    systemProperties System.getProperties()
}

test {

    testLogging {
        exceptionFormat = 'full'
    }

    jacoco {
        excludes = [ "*Test*" ]
    }
}

sourceSets {
    test {
        resources {
            srcDir 'src/test/java'
        }
    }
}
