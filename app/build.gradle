import java.nio.file.Paths
import org.apache.tools.ant.filters.ReplaceTokens;

buildscript {
    repositories {
        jcenter()
    }
}

apply plugin: 'distribution'
apply from: "$rootDir/gradle/javaModule.gradle"

archivesBaseName = 'crate-app'
group = "io.crate"


configurations {
    compileNotTransitive {
        transitive = false
    }
}

evaluationDependsOn(':server')
evaluationDependsOn(':libs:guice')
evaluationDependsOn(':libs:sql-parser')
evaluationDependsOn(':libs:dex')
evaluationDependsOn(':libs:shared')
evaluationDependsOn(':plugins:dns-discovery')

dependencies {
    compile project(':server')
    compile project(':plugins:azure-discovery')
    compile project(':plugins:dns-discovery')
    compile project(':plugins:es-discovery-ec2')
    compile project(':plugins:es-repository-url')
    compile project(':plugins:es-repository-s3')
    compile project(':plugins:es-repository-azure')
    compile project(':plugins:es-analysis-common')
    compile project(':plugins:es-analysis-phonetic')
    compile('org.apache.xbean:xbean-finder:4.5') {
        exclude group: 'org.apache.xbean', module: 'xbean-asm-util'
    }

    implementation "com.google.code.findbugs:jsr305:${versions.jsr305}"

    // include the extensions module in classpath to be able to use it with Crate launched from within intellij
    runtime project(':extensions:functions')
    runtime project(':extensions:lang-js')

    testCompile project(path: ':server', configuration: 'testOutput')
    testCompile("org.postgresql:postgresql:${versions.jdbc}")
    testImplementation "org.apache.lucene:lucene-test-framework:${versions.lucene}"
    testImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${versions.jackson}"
    testImplementation "org.hamcrest:hamcrest:${versions.hamcrest}"
    testImplementation "org.mockito:mockito-core:${versions.mockito}"
    testImplementation "org.junit.jupiter:junit-jupiter:${versions.junit5}"
    testImplementation "junit:junit:${versions.junit}"
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine") {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }
    testImplementation project(path: ':libs:dex', configuration: 'testOutput')

    compileNotTransitive project(':server')
    compileNotTransitive project(':libs:sql-parser')
    compileNotTransitive project(':libs:pgwire')
    compileNotTransitive project(':libs:dex')
    compileNotTransitive project(':libs:shared')
    compileNotTransitive project(':libs:cli')
    compileNotTransitive project(':libs:guice')
    compileNotTransitive project(':libs:es-x-content')
    compileNotTransitive project(':libs:es-plugin-classloader')
    compileNotTransitive project(':plugins:dns-discovery')
    compileNotTransitive project(':plugins:es-discovery-ec2')
    compileNotTransitive project(':plugins:es-repository-url')
    compileNotTransitive project(':plugins:es-repository-s3')
    compileNotTransitive project(':plugins:es-repository-azure')
    compileNotTransitive project(':plugins:es-analysis-common')
    compileNotTransitive project(':plugins:es-analysis-phonetic')
}

task release(dependsOn: 'distTar') {
    doLast {
        logger.info("released version: " + project(':server').getVersion.version)
    }
}

task releaseZip(dependsOn: 'distZip') {
    doLast {
        logger.info("released version: " + project(':server').getVersion.version)
    }
}

task nightly(dependsOn: 'distTar') {
    doLast {
        logger.info("nightly version: " + project(':server').getVersion.version)
    }
}

task writeBuildInfo(dependsOn: [':server:getVersion']) {
    doLast {
        def hash = "git rev-parse HEAD".execute().in.text.trim()

        def file = new File(project.projectDir.path + "/src/main/resources/crate-build.properties")
        if (!file.isFile()) {
            def parentDir = new File(file.parent)
            if (!parentDir.isDirectory()) {
                parentDir.mkdirs()
            }
            file.createNewFile()
        }

        copy {
            into('src/main/dist/bin')
            from('src/bin') {
                filter(ReplaceTokens, tokens: [version: project(':server').getVersion.version])
            }
        }

        ant.propertyfile(file: file.path, comment: 'Build Properties') {
            ant.entry(key: 'hash', value: hash)
            ant.entry(key: 'timestamp', value: System.currentTimeMillis())
        }
    }
}

processResources.dependsOn('writeBuildInfo')

task dist {
    dependsOn ':blackbox:itest', distTar, distZip
}

ext {
    downloadDir = new File(buildDir, 'downloads')
}

def rootDir = project.parent.projectDir

// For releases choose the file under the release_notes structure
// and for all other builds choose CHANGES.txt which contains the unreleased changes
task chooseReleaseNotes(dependsOn: [':server:getVersion']) {
    doLast {
        def version = project(':server').getVersion.version
        def releaseNotesDir = "$rootDir/blackbox/docs/appendices/release-notes"
        def releaseNotesFile = version.replaceAll('-.*', '') + '.rst'
        if (new File(releaseNotesDir + "/" + releaseNotesFile).exists()) {
            copy {
                from("$releaseNotesDir") {
                    include "$releaseNotesFile"
                }
                into project.buildDir.path + "/releaseNotes"
                rename releaseNotesFile, 'CHANGES.txt'
            }
        } else {
            copy {
                from("$rootDir") {
                    include 'CHANGES.txt'
                }
                into project.buildDir.path + "/releaseNotes"
            }
        }
    }
}

tasks.withType(Tar) {
    dependsOn ':server:getVersion'
    dependsOn 'chooseReleaseNotes'
    dependsOn 'createCrateNodeScripts'
    compression = Compression.GZIP
    extension = 'tar.gz'
}

tasks.withType(Zip) {
    dependsOn ':server:getVersion'
    dependsOn 'chooseReleaseNotes'
    dependsOn 'createCrateNodeScripts'
}

installDist.dependsOn(chooseReleaseNotes)

def jdkCopySpec = copySpec {
    from project.jdks.runtime
    exclude "demo/**"
    eachFile { details ->
        if (details.name == 'src.zip') {
            details.exclude()
        }
        details.mode = 0755
    }
}

distributions {
    main {
        baseName = 'crate'
        contents {
            into("") {
                from("$rootDir") {
                    include 'LICENSE'
                    include 'NOTICE'
                    include 'README.rst'
                }
                from("src/main/dist")
                from(project.buildDir.path + "/releaseNotes") {
                    include 'CHANGES.txt'
                }
                File.createTempDir().with {
                    def tmpLogs = new File(absolutePath, 'logs')
                    tmpLogs.mkdirs()
                    from(absolutePath) {
                        includeEmptyDirs = true
                    }
                }
            }
            into("bin") {
                from(project.buildDir.path + "/bin")
            }
            into("lib") {
                from project.configurations.runtime
                exclude 'crate-*.jar'
                exclude 'es.jar'
                exclude 'es-*.jar'
            }
            into("lib") {
                from jar
            }
            into('plugins') {
                from('plugins') {
                    includeEmptyDirs = true
                }
            }
            into('lib/site') {
                from('adminui/_site')
            }
            into('jdk') {
                with jdkCopySpec
            }
        }
    }
}

jar {
    dependsOn ':server:getVersion'

    doFirst {
        manifest.attributes 'Implementation-Version': project(':server').getVersion.version
        // Needed by ES which validates those manifest attributes in Build class
        manifest.attributes 'Build-Date': project(':server').getVersion.buildDate
        manifest.attributes 'Change': project(':server').getVersion.buildShortHash
        project.version = project(':server').getVersion.version
        def serviceDir = file("$buildDir/META-INF/services")
        serviceDir.deleteDir()
        serviceDir.mkdirs()

        // copy all service files from deps to buildDir
        for (file in configurations.compileNotTransitive) {
            zipTree(file).matching { include 'META-INF/services/*' }.each { f ->
                new File(serviceDir, f.name) << f.getText("UTF-8") << "\n"
            }
        }
    }

    archiveFileName = 'crate-app.jar'
    from(configurations.compileNotTransitive.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude 'META-INF/**' // Don't let Gradle merge service files
    }

    // include service files from the buildDir
    from fileTree(buildDir).matching { include 'META-INF/services/*' }
}

task downloadPlugins(
    dependsOn: ['downloadAdminUI',
                ':plugins:es-repository-hdfs:jar',
                ':plugins:azure-discovery:jar',
                ':extensions:jmx-monitoring:jar',
                ':extensions:lang-js:jar',
                ':extensions:functions:jar']) {
    doLast {
        copy {
            from("${buildDir}/crate-admin/")
            into 'adminui'
        }

        copy {
            from(project(':plugins:es-repository-hdfs').buildDir.path + '/libs')
            from(project(':plugins:es-repository-hdfs').buildDir.path + '/plugin-descriptor.properties')
            into 'plugins/es-repository-hdfs'
        }

        copy {
            from(project(':plugins:azure-discovery').tasks.jar.archivePath)
            from(project(':plugins:azure-discovery').buildDir.path + '/tmp/plugin-descriptor.properties')
            into 'plugins/crate-azure-discovery'
        }
        copy {
            from(project(':extensions:jmx-monitoring').tasks.jar.archivePath)
            from(project(':extensions:jmx-monitoring').buildDir.path + '/tmp//plugin-descriptor.properties')
            into 'plugins/crate-jmx-monitoring'
        }
        copy {
            from(project(':extensions:lang-js').tasks.jar.archivePath)
            from(project(':extensions:lang-js').buildDir.path + '/tmp/plugin-descriptor.properties')
            into 'plugins/crate-lang-js'
        }
        copy {
            from(project(':extensions:functions').tasks.jar.archivePath)
            from(project(':extensions:functions').buildDir.path + '/tmp/plugin-descriptor.properties')
            into 'plugins/crate-functions'
        }
    }
}

downloadPlugins.outputs.dir('plugins')


task downloadAdminUI {
    doLast {
        def dest
        if (gradle.taskGraph.hasTask(':app:nightly')) {
            dest = download(
                downloadDir,
                "https://cdn.crate.io/downloads/releases/nightly/crate-admin-latest.tar.gz",
                "crate-admin-latest.tar.gz"
            )
        } else {
            dest = download(
                downloadDir,
                "https://cdn.crate.io/downloads/releases/crate-admin-${versions.crate_admin_ui}.tar.gz",
                "crate-admin-${versions.crate_admin_ui}.tar.gz"
            )
        }
        extractTopfolder(dest, "${buildDir}/crate-admin/_site")
    }
}


processResources.dependsOn(downloadPlugins)

task(runDebug, dependsOn: 'classes', type: JavaExec) {
    main = 'io.crate.bootstrap.CrateDB'
    debug = true
    enableAssertions = true
    classpath = sourceSets.main.runtimeClasspath
    systemProperties += ['es.path.home': "${rootDir}/sandbox/crate"]
    systemProperties System.getProperties()
}

task(run, dependsOn: 'classes', type: JavaExec) {
    main = 'io.crate.bootstrap.CrateDB'
    debug = false
    enableAssertions = true
    classpath = sourceSets.main.runtimeClasspath
    systemProperties += ['es.path.home': "${rootDir}/sandbox/crate"]
    systemProperties System.getProperties()
}

task createCrateNodeScripts(type: CreateStartScripts) {
    outputDir = file('build/bin')
    mainClassName = 'org.elasticsearch.cluster.coordination.NodeToolCli'
    applicationName = 'crate-node'
    classpath = sourceSets.main.runtimeClasspath + files('lib/crate-app.jar')


    // Helper functions for manipulating start script contents.
    ext.insertBefore = { String originalString, String needleString, String insertString ->
        StringBuilder builder = new StringBuilder(originalString);
        Integer position = builder.indexOf(needleString);
        builder.insert(position, insertString);
        return builder.toString();
    }

    ext.insertAfter = { String originalString, String needleString, String insertString ->
        StringBuilder builder = new StringBuilder(originalString);
        Integer position = builder.indexOf(needleString) + needleString.length();
        builder.insert(position, insertString);
        return builder.toString();
    }

    doLast {

        // Unix: Prevent using the system-wide Java installation.
        unixScript.text = insertAfter(unixScript.text, '#!/usr/bin/env sh\n', 'unset JAVA_HOME\n');

        // Unix: Use the bundled Java runtime.
        unixScript.text = unixScript.text.replace('JAVACMD="java"', """
            if \${darwin}; then
                JAVACMD="\${APP_HOME}/jdk/Contents/Home/bin/java"
            else
                JAVACMD="\${APP_HOME}/jdk/bin/java"
            fi
        """)
        unixScript.text = unixScript.text.replace('which java', 'which ${JAVACMD}')

        // Windows: Prevent using the system-wide Java installation.
        windowsScript.text = insertBefore(windowsScript.text, 'if defined JAVA_HOME', 'set JAVA_HOME=\n');

        // Windows: Use the bundled Java runtime.
        windowsScript.text = windowsScript.text.replace('set JAVA_EXE=java.exe', 'set JAVA_EXE=%APP_HOME%/jdk/bin/java.exe')
    }
}

test {
    jacoco {
        excludes = ["*Test*"]
    }
}


clean.dependsOn(['cleanDownloadPlugins'])


def extractTopfolder(File src, String trg) {
    copy {
        includeEmptyDirs = false
        FileTree ft
        if (src.name.endsWith('.tar.gz')) {
            ft = tarTree(src)
        }
        from(ft) {
            eachFile {
                details -> details.path = details.path.substring(details.relativePath.segments[0].length())
            }
        }
        into trg
    }
}
